buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:2.0.3'
    }
}

group 'ch.mobi.pyt'
version '1.0-SNAPSHOT'

apply plugin: 'java'
apply plugin: 'application'

/*
 * mainClassName must be specified before applying shadow plugin
 * see https://github.com/johnrengelman/shadow/issues/336
 */
mainClassName = 'ch.mobi.pyt.Controller'
apply plugin: 'com.github.johnrengelman.shadow'

sourceCompatibility = 1.8
targetCompatibility = 1.8

ext {
    akkaVersion = '2.5.11'
    akkaHttpVersion = '10.1.1'
}

repositories {
    mavenCentral()
}

dependencies {
    compile group: 'com.typesafe.akka', name: 'akka-actor_2.12', version: akkaVersion
    compile group: 'com.typesafe.akka', name: 'akka-http_2.12', version: akkaHttpVersion
    compile group: 'com.typesafe.akka', name: 'akka-slf4j_2.12', version: akkaVersion
    compile group: 'com.typesafe.akka', name: 'akka-stream_2.12', version: akkaVersion
    compile group: 'ch.qos.logback', name: 'logback-core', version: '1.2.3'
    compile group: 'org.slf4j', name: 'slf4j-log4j12', version: '1.7.25'

    testCompile group: 'io.rest-assured', name: 'rest-assured', version: '3.0.7'
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

shadowJar {
    baseName = 'pythagoras'
    classifier = null
    version = null

    transform(com.github.jengelman.gradle.plugins.shadow.transformers.AppendingTransformer) {
        resource = 'reference.conf'
    }
}